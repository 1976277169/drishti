# Copyright (c) 2015, Ruslan Baratov
# Copyright (c) 2016, David Hirvonen
# All rights reserved.

string(COMPARE EQUAL "@ADB_COMMAND@" "" is_empty)
if(is_empty)
  message(FATAL_ERROR "Expected ADB_COMMAND")
endif()

string(COMPARE EQUAL "${APP_SOURCE}" "" is_empty)
if(is_empty)
  message(FATAL_ERROR "Expected APP_SOURCE")
endif()

string(COMPARE EQUAL "@APP_DESTINATION@" "" is_empty)
if(is_empty)
  message(FATAL_ERROR "Expected APP_DESTINATION")
endif()

string(COMPARE EQUAL "@APP_DESTINATION_DIR@" "" is_empty)
if(is_empty)
  message(FATAL_ERROR "Expected APP_DESTINATION_DIR")
endif()

# Make sure the specified directory exists
set(cmd "@ADB_COMMAND@" shell mkdir -p "@APP_DESTINATION_DIR@")
execute_process(COMMAND ${cmd} RESULT_VARIABLE result)
if(NOT result EQUAL 0)
  message(FATAL_ERROR "Command failed: ${cmd}")
endif()

string(COMPARE EQUAL "@x_DEPENDENCIES@" "" is_empty)
if(NOT is_empty)
  # Copy any required dependencies into the working directory
  foreach(local_file @x_DEPENDENCIES@)
    if(EXISTS "${local_file}")
      message("DEPENDENCY: ${local_file} -> @APP_DESTINATION_DIR@/")
      set(cmd @ADB_COMMAND@ push "${local_file}" "@APP_DESTINATION_DIR@/")
      execute_process(COMMAND ${ADB_COMMAND} ${cmd} RESULT_VARIABLE result)
      if(NOT result EQUAL 0)
        message(FATAL_ERROR "Command failed: ${cmd}")
      endif()
    endif()
  endforeach()
endif()

message("Push application to device: ${APP_SOURCE} -> @APP_DESTINATION@")
set(cmd "@ADB_COMMAND@" push "${APP_SOURCE}" "@APP_DESTINATION@")
execute_process(COMMAND ${cmd} RESULT_VARIABLE result)
if(NOT result EQUAL 0)
  message(FATAL_ERROR "Command failed: ${cmd}")
endif()

message("Set executable permissions")
set(cmd "@ADB_COMMAND@" shell chmod 777 "@APP_DESTINATION@")
execute_process(COMMAND ${cmd} RESULT_VARIABLE result)
if(NOT result EQUAL 0)
  message(FATAL_ERROR "Command failed: ${cmd}")
endif()

set(app_arguments "@APP_ARGUMENTS@")
set(arguments)
foreach(arg ${app_arguments})
  set(arguments "${arguments} \"${arg}\"")
endforeach()

message("Set working directory: \"@APP_DESTINATION_DIR@\"")
message("Run application: \"@APP_DESTINATION@\" ${arguments}")
execute_process(
    COMMAND
    "@ADB_COMMAND@" shell "cd \"@APP_DESTINATION_DIR@\"; export LD_LIBRARY_PATH=\"@APP_DESTINATION_DIR@\"; \"@APP_DESTINATION@\" ${arguments}; echo $?"
    RESULT_VARIABLE result
    OUTPUT_VARIABLE output
    ERROR_VARIABLE output
    OUTPUT_STRIP_TRAILING_WHITESPACE
    ERROR_STRIP_TRAILING_WHITESPACE
)
if(NOT result EQUAL 0)
  message(FATAL_ERROR "Command failed")
endif()

message("Command output (with exit code):")
message("*** BEGIN ***")
message("${output}")
message("*** END ***")

string(REGEX REPLACE ".*\n" "" last_output_line "${output}")
string(COMPARE EQUAL "${last_output_line}" "0" is_ok)
if(NOT is_ok)
  message(
      FATAL_ERROR
      "Command exits with error code: ${last_output_line}"
  )
endif()

if(@x_CLEAN@)
  message("Clean-up")
  set(cmd "@ADB_COMMAND@" shell rm "@APP_DESTIONATION_DIR@/*")
  execute_process(COMMAND ${cmd} RESULT_VARIABLE result)
  if(NOT result EQUAL 0)
    message(FATAL_ERROR "Command failed: ${cmd}")
  endif()
endif()

message("Done")
